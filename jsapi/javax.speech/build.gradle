apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
    maven {
        url "http://repository.ops4j.org/maven2/"
    }
}

dependencies {
    compile group: 'javax.microedition', name: 'cldc', version: '1.0'
    compile group: 'javax.microedition', name: 'midp', version: '1.0'

    compile project(':javax.speech.security')
}

compileJava {
	sourceCompatibility = 1.2
	targetCompatibility = 1.1

    options.bootClasspath = configurations.compile
}

jar {
    manifest {
        attributes("Implementation-Title": "JSR 113 - Java Speech API 2.0",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": version)
    }
    baseName 'javax.speech'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        licenses {
          license {
            name 'GNU Lesser General Public License, Version 2.1'
            url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
          }
        }

        developers {
          developer {
            id 'dirk'
            name 'Dirk Schnelle-Walka'
            email 'dirk.schnelle@jvoicexml.org'
          }
        }
      }
    }
  }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


repositories {
    mavenCentral()
    maven {
        url "http://repository.ops4j.org/maven2/"
    }
}


dependencies {
    compile project(':org.jvoicexml.jsapi2')
}

task generateJniHeaders(type: Exec) {
    dependsOn classes
    
    def FileCollection classpath = files(sourceSets.main.compileClasspath,
                                         sourceSets.main.output.classesDir) 
    def nativeIncludes = "$buildDir/include"
    
    commandLine "javah", "-d", nativeIncludes,
        "-classpath", classpath.getAsPath(),
        "org.jvoicexml.jsapi2.sapi.SapiEngineListFactory",
        "org.jvoicexml.jsapi2.sapi.SapiEngineListFactory",
        "org.jvoicexml.jsapi2.sapi.logging.Log4CPlus2JavaLoggingAdapter",    
        "org.jvoicexml.jsapi2.sapi.recognition.SapiRecognizer",
        "org.jvoicexml.jsapi2.sapi.recognition.SapiRecognitionThread",
        "org.jvoicexml.jsapi2.sapi.synthesis.SapiSynthesizer"
}

jar {
    dependsOn generateJniHeaders
    
    manifest {
        attributes("Implementation-Title": "SAPI JSAPI2 implementation",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": version)
    }
    baseName 'org.jvoicexml.jsapi2.sapi'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        licenses {
          license {
            name 'GNU Lesser General Public License, Version 2.1'
            url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
          }
        }

        developers {
          developer {
            id 'dirk'
            name 'Dirk Schnelle-Walka'
            email 'dirk.schnelle@jvoicexml.org'
          }
        }
      }
    }
  }
}



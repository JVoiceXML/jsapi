apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


repositories {
    mavenCentral()
}


dependencies {
    compile group: 'org.mozilla', name: 'rhino', version: '1.7R4'

    compile fileTree(dir: './3rdparty/freetts1.2/lib', include: ['*.jar'])

    compile project(':org.jvoicexml.jsapi2')
}


jar {
    manifest {
        attributes("Implementation-Title": "FreeTTS JSAPI2 implementation",
                   "Implementation-Vendor": "JVoiceXML group",
                   "Implementation-Version": version)
    }
    baseName 'org.jvoicexml.jsapi2.freetts'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        licenses {
          license {
            name 'GNU Lesser General Public License, Version 2.1'
            url 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
          }
        }

        developers {
          developer {
            id 'dirk'
            name 'Dirk Schnelle-Walka'
            email 'dirk.schnelle@jvoicexml.org'
          }
        }
      }
    }
  }
}


